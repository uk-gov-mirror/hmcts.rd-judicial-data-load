buildscript {
    ext {
        springBootVersion = '2.7.7'
    }
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.4.34")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'pmd'
    id "com.github.ben-manes.versions" version "0.41.0"
    id 'info.solidsoft.pitest' version '1.7.4'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.sonarqube' version '2.8'
    id "org.flywaydb.flyway" version "6.2.0"
    id 'uk.gov.hmcts.java' version '0.12.39'
    id 'org.owasp.dependencycheck' version '8.0.1'
}

apply plugin: 'java'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: 'project-report'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: "io.spring.dependency-management"


def versions = [
        lombok             : '1.18.22',
        reformLogging      : '5.1.9',
        serenity           : '2.2.12',
        sonarPitest        : '0.5',
        springBoot         : '2.7.11',
        springHystrix      : '2.2.8.RELEASE',
        jackson            : '2.14.2',
        launchDarklySdk    : '5.10.7',
        camel              : '3.8.0',
        log4j              : '2.18.0',
        springVersion      : '5.3.27',
        feign              : '3.8.0',
        logback            : '1.2.10',
        netty              : '1.1.2',
        bytebuddy          : '1.12.7'
]

mainClassName = 'uk.gov.hmcts.reform.juddata.JudicialApplication'
def jarName = 'rd-judicial-data-load.jar'

group = 'uk.gov.hmcts.reform.juddata'
version = '0.0.1'

tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

sourceSets {

    functionalTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/functionalTest/java')
        }
        resources.srcDir file('src/functionalTest/resources')
    }

    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDirs = [file('src/integrationTest/resources'), file ('src/functionalTest/resources')]
    }

    smokeTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/smokeTest/java')
        }
        resources.srcDir file('src/smokeTest/resources')
    }
}

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
        testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
        testSourceDirs += project.sourceSets.functionalTest.resources.srcDirs
        testSourceDirs += project.sourceSets.smokeTest.java.srcDirs
        testSourceDirs += project.sourceSets.smokeTest.resources.srcDirs
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    functionalTestCompile.extendsFrom testCompile
    functionalTestRuntime.extendsFrom testRuntime

}
check.dependsOn jacocoTestCoverageVerification

pmd {
    toolVersion = '6.53.0'
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.smokeTest, sourceSets.integrationTest]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSets = [
            'category/java/errorprone.xml',
            'category/java/bestpractices.xml'
    ]
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task functional(type: Test) {
    description = "Runs functional tests"
    group = "Verification"
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    finalizedBy aggregate
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
    testClassesDirs = sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
}

task migratePostgresDatabase(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
    baselineOnMigrate = true
    if (project.hasProperty("dburl")) {
        url = "jdbc:postgresql://${dburl}"
    }
}

task fortifyScan(type: JavaExec)  {
  mainClass.set("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
  // This is a temporary fix to prevent the nightly build from failing if the Fortify scan detects issues 
  ignoreExitValue = true
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    executionData(test)
    reports {
        xml.required = true
        csv.required = false
        xml.outputLocation = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}


pitest {
    junit5PluginVersion = '0.15'
    targetClasses = ['uk.gov.hmcts.reform.juddata.*']
    testSourceSets = [sourceSets.integrationTest, sourceSets.test]
    excludedClasses = [
            'uk.gov.hmcts.reform.juddata.configuration.*',
            'uk.gov.hmcts.reform.juddata.JudicialApplication',
            'uk.gov.hmcts.reform.juddata.camel.util.JrdExecutor',
            'uk.gov.hmcts.reform.juddata.client.*',
            'uk.gov.hmcts.reform.juddata.response.*',
            'uk.gov.hmcts.reform.juddata.camel.util.JobStatus',
            'uk.gov.hmcts.reform.juddata.camel.util.JrdConstants',
            'uk.gov.hmcts.reform.juddata.camel.util.PublishJudicialData',
            'uk.gov.hmcts.reform.juddata.camel.util.JrdExecutor',
            'uk.gov.hmcts.reform.juddata.camel.servicebus.TopicPublisher',
            'uk.gov.hmcts.reform.juddata.camel.util.JrdUserProfileUtil*'
    ]
    threads = 6
    enableDefaultIncrementalAnalysis = true
    historyInputLocation = 'build/reports/pitest/fastermutationtesting'
    historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 80
    useClasspathFile = true
}


project.tasks['pitest'].group = "Verification"

sonarqube {
    properties {
        property "sonar.projectName", "RD :: Judicial Reference Data load"
        property "sonar.projectKey", "JUDDATA"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"

        property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/juddata/configuration/**," +
                "src/main/java/uk/gov/hmcts/reform/elinks/configuration/**," +
                "src/main/java/uk/gov/hmcts/reform/juddata/JudicialApplication.java," +
                "src/main/java/uk/gov/hmcts/reform/juddata/client/**," +
                "src/main/java/uk/gov/hmcts/reform/juddata/response/**," +
                "src/main/java/uk/gov/hmcts/reform/juddata/camel/binder/**,"
    }
}

project.tasks['sonarqube'].dependsOn test, jacocoTestReport
project.tasks['sonarqube'].group = "Verification"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

dependencies {


    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: versions.springBoot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache', version: versions.springBoot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-mail', version: versions.springBoot

    implementation group: 'org.springframework', name: 'spring-core', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-beans', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-aop', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-aspects', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-context', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-context-support', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-expression', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-jcl', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-jdbc', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-orm', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-tx', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-web', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-webmvc', version: versions.springVersion

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-batch', version: versions.springBoot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-freemarker', version: versions.springBoot

    implementation group: 'org.springframework.security', name: 'spring-security-core', version: '5.7.5'
    implementation group: 'org.bouncycastle', name: 'bcpkix-jdk18on', version: '1.72'


    implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.0.5'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    implementation group: 'com.fasterxml.jackson', name: 'jackson-bom', version: '2.14.2', ext: 'pom'
    implementation (group: 'com.fasterxml.jackson.core', name: 'jackson-core') {
        version {
            strictly versions.jackson
        }
    }
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson
    implementation 'com.github.hmcts:idam-java-client:2.0.1'

    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.glassfish', module: 'jakarta.el'
    }

    implementation "io.github.openfeign:feign-httpclient:11.8"
    implementation group: 'io.github.openfeign.form', name: 'feign-form', version: versions.feign
    implementation group: 'io.github.openfeign.form', name: 'feign-form-spring', version: versions.feign

    implementation group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '3.0.2'

    implementation "com.github.hmcts.java-logging:logging:${versions.reformLogging}"
    implementation "com.github.hmcts.java-logging:logging-appinsights:${versions.reformLogging}"
    implementation 'com.github.hmcts:service-auth-provider-java-client:5.0.0'

    implementation ('com.github.hmcts:data-ingestion-lib:0.5.2.4') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-web'
    }
    //Fix for CVE-2021-29425
    implementation 'commons-io:commons-io:2.11.0'
    implementation group: 'org.apache.camel', name: 'camel-bom', version: versions.camel, ext: 'pom'
    implementation group: 'org.apache.camel.springboot', name: 'camel-spring-boot-dependencies', version: versions.camel

    testImplementation group: 'com.microsoft.azure', name: 'azure-storage-blob', version: '11.0.0'

    implementation group: 'org.flywaydb', name: 'flyway-core', version: '5.2.4'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.5.1'

    implementation group: 'com.google.guava', name: 'guava', version: '30.1-jre'

    implementation group: 'org.apache.commons', name: 'commons-vfs2', version: '2.4.1'
    implementation group: 'com.jcraft', name: 'jsch', version: '0.1.55'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'

    implementation group: 'com.opencsv', name: 'opencsv', version: '5.5.2'
    implementation group: 'commons-lang', name: 'commons-lang', version: '2.6'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'

    implementation group: 'commons-configuration', name: 'commons-configuration', version: '1.10'
    implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: versions.launchDarklySdk

    implementation group: 'com.azure', name: 'azure-core', version: '1.35.0'
    implementation group: 'com.azure', name: 'azure-messaging-servicebus', version: '7.11.0'
    implementation group: 'io.projectreactor.netty', name: 'reactor-netty-core', version: versions.netty
    implementation group: 'io.projectreactor.netty', name: 'reactor-netty-http', version: versions.netty
    implementation group: 'io.projectreactor', name: 'reactor-core', version: '3.4.14'
    
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: versions.log4j
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: versions.log4j
    implementation group: 'org.apache.logging.log4j', name: 'log4j', version: versions.log4j
    implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: versions.log4j

    implementation group: 'ch.qos.logback', name: 'logback-core', version: versions.logback
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-config-server', version: '3.1.5'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.1.5'

    // Fix for CVE-2022-45047
    implementation group: 'org.apache.sshd', name: 'sshd-common', version: '2.9.2'
    implementation group: 'org.apache.sshd', name: 'sshd-core', version: '2.9.2'
    implementation group: 'org.apache.sshd', name: 'sshd-osgi', version: '2.9.2'
    implementation group: 'org.apache.sshd', name: 'sshd-sftp', version: '2.9.2'

    implementation group: 'io.github.openfeign', name: 'feign-jackson', version: '12.1'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    functionalTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    functionalTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    smokeTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    smokeTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    testImplementation("org.hamcrest:hamcrest-junit:2.0.0.0") {
        exclude group: "org.hamcrest", module: "hamcrest-core"
        exclude group: "org.hamcrest", module: "hamcrest-library"
    }

    testImplementation (group: 'io.rest-assured', name: 'rest-assured', version: '4.1.2') {
        exclude group: "com.sun.xml.bind", module: "jaxb-osgi"
    }
    testImplementation group: 'com.h2database', name: 'h2'
    testImplementation ("org.mockito:mockito-core:4.3.1") {
        exclude group: "net.bytebuddy", module: "byte-buddy"
        exclude group: "net.bytebuddy", module: "byte-buddy-agent"
    }
    testImplementation ("org.mockito:mockito-inline:4.3.1") {
        exclude group: "net.bytebuddy", module: "byte-buddy"
        exclude group: "net.bytebuddy", module: "byte-buddy-agent"
    }
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: versions.bytebuddy
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy-agent', version: versions.bytebuddy

    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.3.1'


    testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.7'
    testImplementation group: 'org.powermock', name: 'powermock-mockito-release-full', version: '1.5.4', ext: 'pom'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot

    testImplementation group: 'org.springframework.batch', name: 'spring-batch-test', version: '4.3.7'

    testImplementation 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5'
    testImplementation group: 'org.javatuples', name: 'javatuples', version: '1.2'

    testImplementation(group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity) {
        exclude group: 'com.vladsch.flexmark', module: 'flexmark-all'
        exclude group: 'javax.websocket', module: 'javax.websocket-api'
    }
    testImplementation(group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity) {
        exclude group: 'com.vladsch.flexmark', module: 'flexmark-all'
        exclude group: 'javax.websocket', module: 'javax.websocket-api'
    }
    testImplementation(group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity) {
        exclude group: 'com.vladsch.flexmark', module: 'flexmark-all'
        exclude group: 'javax.websocket', module: 'javax.websocket-api'
    }

    testImplementation(group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity) {
        exclude group: 'com.vladsch.flexmark', module: 'flexmark-all'
        exclude group: 'javax.websocket', module: 'javax.websocket-api'
    }

    testImplementation group: 'org.apache.camel', name: 'camel-test-spring-junit5', version: versions.camel
    testImplementation group: 'org.apache.camel', name: 'camel-test-junit5', version: versions.camel



    testImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.16.2'
    testImplementation group: 'org.postgresql', name: 'postgresql', version: '42.4.1'
    testImplementation 'com.github.hmcts:fortify-client:1.2.0:all'

	implementation group: 'org.yaml', name: 'snakeyaml', version: '1.33'

    functionalTestImplementation sourceSets.main.runtimeClasspath
    functionalTestImplementation sourceSets.test.runtimeClasspath

    integrationTestImplementation sourceSets.main.runtimeClasspath
    integrationTestImplementation sourceSets.test.runtimeClasspath

    smokeTestImplementation sourceSets.main.runtimeClasspath
    smokeTestImplementation sourceSets.test.runtimeClasspath
}

dependencyManagement {

    dependencies {
        // CVE-2021-42340
        dependencySet(group: 'org.apache.tomcat.embed', version: '9.0.71') {
            entry 'tomcat-embed-core'
            entry 'tomcat-embed-el'
            entry 'tomcat-embed-websocket'
        }
	
     //CVE-2022-40152
     dependencySet(group: 'com.fasterxml.woodstox', version: '6.5.0') {
            entry 'woodstox-core'
     }

        //CVE-2021-22044
        dependencySet(group: 'org.springframework.cloud', version: '3.1.5') {
            entry 'spring-cloud-starter-openfeign'
            entry 'spring-cloud-openfeign-core'
        }
        //CVE-2023-24998
        dependencySet(group: 'commons-fileupload', version: '1.5') {
            entry 'commons-fileupload'
        }
    }
}

dependencyCheck {
    suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

gradle.startParameter.continueOnFailure = true

bootJar {
    archiveFileName = jarName
    manifest {
        attributes('Implementation-Version': project.version.toString())
    }
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues.
// this setting only applies when running via gradle bootRun -- see web.config for the
// java flag that configures the deployed applications
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]

// Fix for CVE-2021-21295, CVE-2022-41881 & need to be removed with new Azure blob version
configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'io.netty'
                && details.requested.name != 'netty-tcnative-boringssl-static') {
            details.useVersion "4.1.89.Final"
        }

        // Fix for CVE-2020-21913 & needs to be removed when camel-azure-starter is upgraded to latest version in data-ingestion-library
        if (details.requested.group == 'com.ibm.icu') {
            details.useVersion "66.1"
        }
    }
      exclude group: 'org.codehaus.jettison', module: 'jettison'
      exclude group: 'io.netty', module:'netty-tcnative-classes'
      exclude group: 'io.projectreactor.netty.incubator', module:'reactor-netty-incubator-quic'

    //Fix for CVE-2014-3488, CVE-2015-2156, CVE-2019-16869, CVE-2019-20444, CVE-2019-20445,
    // CVE-2021-21290, CVE-2021-21295, CVE-2021-21409, CVE-2021-37136, CVE-2021-37137,
    // CVE-2021-43797, CVE-2022-24823, CVE-2022-41881, CVE-2022-41915
    exclude group: 'io.netty.incubator', module: 'netty-incubator-codec-classes-quic'
    //Fix for CVE-2014-3488, CVE-2015-2156, CVE-2019-16869, CVE-2019-20444
    // CVE-2021-21295,CVE-2019-20445, CVE-2021-21290, CVE-2022-41881, CVE-2022-41915
    // CVE-2021-21409, CVE-2021-37136, CVE-2021-37137, CVE-2021-43797, CVE-2022-24823,
    exclude group: 'io.netty.incubator', module: 'netty-incubator-codec-native-quic'
}

test {
    useJUnitPlatform()
}

functional {
    useJUnitPlatform()
}

integration {
    useJUnitPlatform()
}

rootProject.tasks.named("processSmokeTestResources") {
    duplicatesStrategy = 'include'
}

rootProject.tasks.named("processFunctionalTestResources") {
    duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
    duplicatesStrategy = 'include'
}
